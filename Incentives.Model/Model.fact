namespace Incentives.Model;

fact Individual {
key:
    string anonymousId;

query:
	Profile* profiles {
		IndividualProfile ip : ip.individual = this
		Profile p : p = ip.profile
	}
}

fact IndividualProfile {
key:
	Individual individual;
	Profile profile;
}

fact Profile {
key:
	unique;

mutable:
	string name;

query:
	Activity* activities {
		Activity a : a.profileQuarter.profile = this
	}
}

fact Company {
key:
	string identifier;

query:
	Category* categories {
		Category c : c.company = this
	}
}

fact Quarter {
key:
	Company company;
	date startDate;

query:
	ActivityReward* rewards {
		ActivityReward r : r.quarter = this
	}
}

fact Category {
key:
	Company company;
	string identifier;

mutable:
	string description;
	int ordinal;

query:
	ActivityDefinition* activities {
		ActivityDefinition a : a.category = this
	}
}

fact ActivityDefinition {
key:
	Category category;
	string identifier;

mutable:
	string description;
	string qualifier;
	int ordinal;
}

fact ActivityReward {
key:
	ActivityDefinition definition;
	Quarter quarter;

mutable:
	int points;
}

fact ProfileQuarter {
key:
	Profile profile;
	Quarter quarter;
}

fact Activity {
key:
	date activityDate;
	ProfileQuarter profileQuarter;
	ActivityReward activityReward;

mutable:
	string description;
	int multiplier;
}
